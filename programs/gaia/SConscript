#!/bin/env python

Import('*')

#######################################
# ENVIRONMENT
#######################################
env = env.Copy(QT_LIB = 'qt-mt', tools = ['qt'])

env.Append(CCFLAGS = [ '-DQT_NO_DEBUG', '-DQT_THREAD_SUPPORT', '-DQT_SHARED', '-DQT_TABLET_SUPPORT' ] )
env.Append(CXXFLAGS = [ '-DQT_NO_DEBUG', '-DQT_THREAD_SUPPORT', '-DQT_SHARED', '-DQT_TABLET_SUPPORT' ] )

env.Append(LIBS = [ 'z', 'm', 'jpeg', 'GL', 'GLU', 'pthread' ])
env.Prepend(CPPPATH = [ '../../lib/wwfetch' ])
env.Prepend(LIBPATH = [ '../../lib/wwfetch' ])

parseenv = Environment()
parseenv.ParseConfig('curl-config --cflags --libs')
parseenv.ParseConfig('libpng-config --cflags --libs')

env.Append( LIBS = parseenv['LIBS'] )
env.Append( CPPPATH = parseenv['CPPPATH'] )
env.Append( LIBPATH = parseenv['LIBPATH'] )

if int(env['gpsd']):
	env.Append( LIBS = [ 'gps' ], CCFLAGS = [ '-DWITH_GPSD' ], CXXFLAGS = [ '-DWITH_GPSD' ] )

env.Append(CCFLAGS = [ '-DDATADIR="\\"' + env['prefix'] + "/" + env['datadir'] + '\\""' ] )
env.Append(CXXFLAGS = [ '-DDATADIR="\\"' + env['prefix'] + "/" + env['datadir'] + '\\""' ] )

#######################################
# TARGETS
#######################################
srcs = Split("""Main.cc
		MainWidget.cc
		GLWidget.cc
		ControlWidget.cc
		EarthView.cc
		FilesystemStorage.cc
		FlatEarthView.cc
		GPSLayer.cc
		GPSSourceNMEA.cc
		GPSSourceTest.cc
		GridLayer.cc
		Hud.cc
		HudIcon.cc
		HudObject.cc
		Messages.cc
		PreloadedTextureManager.cc
		RawBuffer.cc
		SimpleTileStorage.cc
		GlobeEarthView.cc
		Layer.cc
		TestLayer.cc
		Texture.cc
		TextureTile.cc
		Tile.cc
		Timer.cc
		Viewpoint.cc
		WorldWindFetcher.cc
		WorldWindLayer.cc
		WorldWindTileManager.cc""")


if int(env['gpsd']):
	srcs += ['GPSSourceGPSD.cc']

env.Program('../../gaia', [srcs, libwwfetch])

if env['prefix'] != '.':
	env.Install(env['prefix'] + "/" + env['bindir'], ['../../gaia']);
	env.Install(env['prefix'] + "/" + env['datadir'], ['../../data/font.png']);
